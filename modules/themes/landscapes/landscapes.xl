// ****************************************************************************
//  landscapes.xl                                                  Tao project 
// ****************************************************************************
// 
//   File Description:
// 
//   Landscapes - A theme containing nice landscape pictures
// 
// 
// 
// 
// 
// 
// 
// 
// ****************************************************************************
// This software is licensed under the GNU General Public License v3
// See file COPYING for details.
//  (C) 2012-2014 Christophe de Dinechin <christophe@taodyne.com>
//  (C) 2012-2014 Jerome Forissier <jerome@taodyne.com>
//  (C) 2012-2014 Baptiste Soulisse <baptiste.soulisse@taodyne.com>
//  (C) 2012-2014 Catherine Burvelle <catherine@taodyne.com>
//  (C) 2012-2014 Taodyne SAS
// ****************************************************************************

module_description
    id "ca09f854-bb99-472e-bd5d-a59b652a22cb"
    name "Landscapes theme"
    description "A theme containing nice landscape pictures"
    import_name "LandscapesTheme"
    author "Taodyne SAS"
    website "http://www.taodyne.com"
    version 1.003

module_description "fr",
    name "Thème Paysages"
    description "Un thème contenant de splendides images de paysages"

import Slides
import VLCAudioVideo
import Animate
import Charts

add_search_path "Landscapes:", module_dir

theme_slide_width  "Landscapes" -> 1920
theme_slide_height "Landscapes" -> 1080

data landscapes // Array of landscapes

// Load pictures from document folder
landscape_index -> 0
landscape_index := 0
landscape with files "landscapes/*"

// If no one, then load from module folder
if(landscape_index = 0) then
    for i in 1..8 loop
        landscape "Landscapes:" & i & ".jpg"

landscape T:text ->
// ----------------------------------------------------------------------------
//   Load and parse landscape pictures
// ----------------------------------------------------------------------------
    landscapes[landscape_index] := T
    landscape_index := landscape_index + 1
landscape Other -> false



// ============================================================================
//
//    Charts
//
// ============================================================================

theme_color "Landscapes", Master:text, "line", N:integer ->
    line_color_hsv ((N - 1) * 65) mod 360, 0.612, 1.0, 0.7

theme_color "Landscapes", "pie", Style:text, N:integer ->
    color_hsv (360.0 * N) / (chart_count chart_first), 0.612, 1.0, 0.7
    line_color "black", 0.3

theme_color "Landscapes", "area", Style:text, N:integer -> theme_color "Landscapes", "bar", Style, N
theme_color "Landscapes", "line", Style:text, N:integer -> theme_color "Landscapes", "bar", Style, N
theme_color "Landscapes", "bar", Style:text, N:integer ->
    color_hsv ((N - 1) * 65) mod 360, 0.612, 1.0, 0.7
    line_color "black", 0.3

theme_fade "Landscapes", "pie", Style:text, N:integer, Index:integer -> fade_at(page_time, (Index + 1))
theme_fade "Landscapes", Master:text, Style:text, N:integer, Index:integer -> fade_at(page_time, N - chart_first + 1)

theme_boxstyle "Landscapes",Master:text,"chart" -> light 0, false; color "white",0.0
theme_boxstyle "Landscapes",Master:text,"pie" -> light 0, false; color "white",0.0

theme_chart "Landscapes", Master:text, Style:text, "3D", W:real, H:real, N:integer, Set:integer, Index:integer ->
// ----------------------------------------------------------------------------
//   Display chart part
// ----------------------------------------------------------------------------
    Ratio := (chart_data_ratio (Set, Index))
    T -> 0.0
    on "pageentry",
        T := 0.0
    T in [0..1]
        time
        T := T + 0.002
    locally
        show (T * 0.7)
        theme_chart "Landscapes", Master, Style, "3D", W, H, N, Set, Index, Ratio * T


theme_chart "Landscapes", Master:text, Style:text, Format:text, W:real, H:real, N:integer, Set:integer, Index:integer ->
// ----------------------------------------------------------------------------
//   Display chart part
// ----------------------------------------------------------------------------
    Ratio := chart_data_ratio (Set, Index)
    R -> 0.0
    on "pageentry",
        R := 0.0
    R in [0..1]
        time
        R := R + 0.002
    locally
        show R
        theme_chart "Landscapes", Master, Style, Format, W, H, N, Set, Index, Ratio * R



// ============================================================================
//
//    Slides
//
// ============================================================================

theme_background "Landscapes", Master:text ->
// ----------------------------------------------------------------------------
//   Display landscape pictures
// ----------------------------------------------------------------------------
    movie_pause "re:"
    clear_color 0, 0, 0, 1
    background -900,
        color "white"
        texture landscapes[(page_number - 1) mod landscape_index]
        rectangle 1920, 1080

theme_size "Landscapes",Master:text,"title"    -> 95
theme_size "Landscapes",Master:text,"subtitle"    -> 60
theme_size "Landscapes",Master:text,"story"        -> 52
theme_size "Landscapes",Master:text,"story",2     -> 40

theme_color "Landscapes",Master:text,"title"     -> color "white"; line_color "black", 0.3; line_width 1

theme_font "Landscapes",Master:text,Style:text     -> font "TeXGyreAdventor", "Yanone Kaffeesatz Regular"

theme_boxstyle "Landscapes",Master:text,Style:text  -> light 0, false; color "white",0.6; line_color "black", 0.30

theme_align "Landscapes",Master:text,"story", N  -> margins 25+50*N, 0

theme_box "Landscapes",Master:text,Style:text, X:real,Y:real,W:real,H:real ->
// ----------------------------------------------------------------------------
//   Draw the background box for text and flows
// ----------------------------------------------------------------------------
    contents -10,
        theme_boxstyle theme, Master, Style
        radial_gradient (W+10)/2, (H+10)/2, (W+H)/2, W+10, H+10,
            gradient_color 0.0, 0.9, 0.9, 1, 0.97
            gradient_color 0.5, 0.9, 0.9, 1, 0.99
            gradient_color 1.0, 0.9, 0.9, 1, 1.0
        rectangle X, Y, W + 10, H + 10


