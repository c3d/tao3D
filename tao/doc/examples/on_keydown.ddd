// Override default shortcut that blanks the screen
key "b" -> nil

on "keydown:Control-X",
    if page_label != "Command prompt" then
        goto_page "Command prompt"
    else
        goto_page prev_page_label

on "keydown:Control-D",
    writeln "Bye!"
    exit 0

page "Rectangle",
    text_box 0, 0, window_width, window_height,
        color "black"
        font "Ubuntu", 24
        align_center
        vertical_align_bottom
        text "Press "
        if is_available mac then text "Cmd-X!" else text "Ctrl-X!"
    color "red"
    rectangle 0, 0, 200, 100

page "Circle",
    color "orange"
    circle 0, 0, 100

page "Separator",
    goto_page page_name (page_number - 1)


CX -> if is_available mac then "<Cmd-X>: " else "<Ctrl-X>:"
Log -> "" 
if Log = "" then
    Log := "Command prompt -" & cr & " " & CX
    Log := Log & " Back to previous page" & cr
    Log := Log & " help:     Show help"
Msg -> ""
MsgLen -> text_length Msg
KPTime -> 0.0
page "Command prompt",
    on "keydown:\S| |Key_Backspace|Key_Return",
        KPTime := page_time
        if key_name = "Key_Backspace" then
            if MsgLen > 0 then
                Msg := text_range Msg, 0, (MsgLen - 1)
        else if key_name = "Key_Return" then
            Log := Log & cr & "> " & Msg
            if Msg = "help" or Msg = "?" then
                Log := Log & cr & "Available commands:" & cr & " back, cls, circ, help, rect"
            else if Msg = "cls" then
                Log := " "
            else if Msg = "circ" then
                goto_page "Circle"
            else if Msg = "rect" then
                goto_page "Rectangle"
            else if Msg = "back" then
                goto_page prev_page_label
            else
                Log := Log & cr & "?"
            Msg := ""
        else
            Msg := Msg & key_text
    clear_color 0.16, 0.16, 0.13, 1.0
    locally
        color 0.17, 0.19, 0.17, 1.0
        rectangle 0, 12.5, window_width - 50, window_height - 75
    font "Courier New", 25
    color "white"
    text_box 0, 12.5, window_width - 50, window_height - 75, 
        vertical_align_bottom
        align_left
        text Log 
    text_box 0, 25 - 0.5 * window_height, window_width - 50, 25,
        align_left
        text "> " & Msg & " " 
        if (integer ((page_time - KPTime) * 1.5)) mod 2 = 0 then
            anchor
                color 0, 0.75, 0.15
                rectangle -6, 8, 12, 24
    
