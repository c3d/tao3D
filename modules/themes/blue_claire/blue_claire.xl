// ****************************************************************************
//  blue_claire.xl                                                 Tao project 
// ****************************************************************************
// 
//   File Description:
// 
//     Blue Claire : A simple blue theme
// 
// 
// 
// 
// 
// 
// 
// 
// ****************************************************************************
// This software is licensed under the GNU General Public License v3
// See file COPYING for details.
//  (C) 2014 Christophe de Dinechin <christophe@taodyne.com>
//  (C) 2012-2014 Jerome Forissier <jerome@taodyne.com>
//  (C) 2012-2014 Baptiste Soulisse <baptiste.soulisse@taodyne.com>
//  (C) 2012-2014 Catherine Burvelle <catherine@taodyne.com>
//  (C) 2012-2014 Claire Hyvrier <claire.hyvrier@taodyne.com>
//  (C) 2014 Taodyne SAS
// ****************************************************************************
//    Background image from http://freestock.ca, used with permission
// ****************************************************************************

import Slides
import Charts

module_description
    id "C3FE1E13-F4EB-4E65-96D9-73C8FBBFC90D" 
    name "BlueClaire theme"
    description "A simple blue theme"
    import_name "BlueClaireTheme"
    author "Taodyne SAS"
    website "http://www.taodyne.com"
    version 1.004

module_description "fr",
    name "Theme BleuClaire"
    description "Un thÃ¨me bleu simple"

// Adding this module's images/ directory to the BlueClaire: search path
// for the current document
add_search_path "BlueClaire:", module_dir



// ============================================================================
// 
//   Theme size
// 
// ============================================================================

theme_slide_width  "BlueClaire" -> 1024
theme_slide_height "BlueClaire" -> 768

theme_drawing_width "BlueClaire" -> 880
theme_drawing_height "BlueClaire" -> 550

// ============================================================================
// 
//    Colors
// 
// ============================================================================

theme_color      "BlueClaire","main","title"            -> color "#960000"
theme_color      "BlueClaire","main","subtitle"         -> color "#264580"
theme_color      "BlueClaire","section","title"         -> color "#5685c0"
theme_color      "BlueClaire","section","subtitle"      -> color "#264580"
theme_color      "BlueClaire",Master:text,"title"       -> color "#960000"
theme_color      "BlueClaire",Master:text,"subtitle"    -> color "#264580"
theme_color      "BlueClaire",Master:text,"story"       -> color "black"
theme_color      "BlueClaire", Master:text, "line", N:integer ->
    T := 0.4 / chart_datasets_count
    line_color_hsv 212, 0.56 - T * N, 0.74 + T * N
theme_color      "BlueClaire", "pie", Style:text, N:integer ->
    T := 0.4 / (chart_count chart_first)
    color_hsv 212, 0.56 - T * N, 0.74 + T * N
theme_color      "BlueClaire", "area", Style:text, N:integer -> theme_color "BlueClaire", "bar", Style, N
theme_color      "BlueClaire", "line", Style:text, N:integer -> theme_color "BlueClaire", "bar", Style, N
theme_color      "BlueClaire", "bar", Style:text, N:integer ->
    T := 0.4 / chart_datasets_count
    color_hsv 212, 0.56 - T * N, 0.74 + T * N

theme_font       "BlueClaire",Master:text,"story"       -> font "TeX Gyre Adventor", "Ubuntu", "Arial"
theme_boxstyle   "BlueClaire",Master:text,"title"       -> color "transparent"
theme_boxstyle   "BlueClaire",Master:text,Style:text    -> color "transparent"

// Size
theme_size "BlueClaire",Master:text,"chart_data"        -> 15
theme_size "BlueClaire",Master:text,"chart_label"       -> 20
theme_size "BlueClaire",Master:text,"chart_tick"        -> 15
theme_size "BlueClaire",Master:text,"chart_title"       -> 25
theme_size "BlueClaire",Master:text,"chart_legend"      -> 20
theme_size "BlueClaire",Master:text,"story"             -> 32
theme_size "BlueClaire",Master:text,"title"             -> 48
theme_size "BlueClaire",Master:text,"subtitle"          -> 32
theme_size "BlueClaire",Master:text,"story",1           -> 32
theme_size "BlueClaire",Master:text,"story",2           -> 28
theme_size "BlueClaire",Master:text,"story",3           -> 24
theme_size "BlueClaire",Master:text,"story",4           -> 22


// ============================================================================
// 
//    Background pictures
// 
// ============================================================================

theme_background "BlueClaire", "main" ->
// ----------------------------------------------------------------------------
//   Background for main title slides
// ----------------------------------------------------------------------------
    clear_color 0.35, 0.52, 0.74, 1
    contents -900,
        color "white"
        image 0, 0, 1.1, 1.1, "BlueClaire:blue-main.png"


theme_background "BlueClaire", "section" ->
// ----------------------------------------------------------------------------
//   Background for section slides
// ----------------------------------------------------------------------------
    clear_color 0.35, 0.52, 0.74, 1
    contents -900,
        color "white"
        image 0, 0, 1.1, 1.1, "BlueClaire:blue-section.png"


theme_background "BlueClaire", Master:text ->
// ----------------------------------------------------------------------------
//   Background for other slides (story, etc)
// ----------------------------------------------------------------------------
    clear_color 0.35, 0.52, 0.74, 1
    contents -900,
        color "white"
        image 0, 0, 1.1, 1.1, "BlueClaire:blue-story.png"



// ============================================================================
// 
//    Slide layout
// 
// ============================================================================

theme_layout "BlueClaire", "main" ->
// ----------------------------------------------------------------------------
//    Flows for main title
// ----------------------------------------------------------------------------
    flows -200, -20, 600, 280, "title", "subtitle"


theme_layout "BlueClaire", "section" ->
// ----------------------------------------------------------------------------
//    Flows for section title
// ----------------------------------------------------------------------------
    flows 0, 50, 700, 250, "title", "subtitle"


theme_layout "BlueClaire", Master:text ->
// ----------------------------------------------------------------------------
//    Centered flows
// ----------------------------------------------------------------------------
    flows 80, 300, 850, 120, "title", "subtitle"
    flow 80, -80, 850, 580, "story"
    flow -140, -80, 400, 580, "left"
    flow  300, -80, 400, 580, "right"


theme_picture "BlueClaire", Master:text, Body ->
// ----------------------------------------------------------------------------
//    Default picture box (keep consistent with layout below)
// ----------------------------------------------------------------------------
    contents 0,
        translate 80, -80, 0
        theme_color "BlueClaire", Master, "picture"
        Body



// ============================================================================
//
//    Charts layouts
//
// ============================================================================

theme_chart_layout "BlueClaire", "pie", Style:text, Format:text ->
// ----------------------------------------------------------------------------
//    Pie chart layout for Autumn theme
// ----------------------------------------------------------------------------
    chart_series 0, -35, 400, 400, "pie", Style, Format

    chart_flow 0, 235, 850, 50, "chart_title"
    chart_flow 350, -25, 250, 400, "chart_legend"

    locally
        translatex 35
        box "chart", 0, 0, chart_width, chart_height


theme_chart_layout "BlueClaire", Master:text, Style:text, Format:text ->
// ----------------------------------------------------------------------------
//    Default chart layout for Autumn theme
// ----------------------------------------------------------------------------

    chart_flow 0, 235, 850, 50, "chart_title"
    chart_flow 350, -25, 250, 400, "chart_legend"

    chart_flow 265, -225, 100, 50, "chart_xlabel"
    chart_flow -325, 200, 100, 50, "chart_ylabel"

    // Draw axis
    chart_xaxis -325, -225, 550, 400, Style, Format
    chart_yaxis -325, -225, 550, 400, Style, Format

    chart_series -325, -225, 550, 400, Master, Style, Format

    locally
        translatex 35
        box "chart", 0, 0, chart_width, chart_height

