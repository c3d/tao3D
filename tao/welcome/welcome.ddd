// ****************************************************************************
//
//                    Default Taodyne welcome screen for 2013
//
//
//                                                  (C) 2010-2013 Taodyne SAS
// ****************************************************************************

import KeyboardTheme

theme "Keyboard"


base_slide "Tao Presentations",
// ----------------------------------------------------------------------------
//   Show the welcome slide for Tao Presentations
// ----------------------------------------------------------------------------

    // Contents of the left column box
    left_column
        // Select the slide language based on default user language
        lang tao_language

        // Display a simple bullet point
        * "Discover our all new themes and samples in the Help menu" fr "Découvrez nos nouveaux thèmes et exemples dans le menu Aide"
        --

        // Display a more complicated computed bullet point
        english
           show_menu "Select ", "View->Document Source", " to see how this page is done"
        french
            show_menu "Choisissez ", "Affichage->Source du document", " pour voir comment cette page est réalisée"
        show_menu Action, Menu, Details ->
            * Action
            text_span
                color "#036"
                italic
                text Menu
            text Details

        // Non-bullet text and line separators
        --
        --
        --
        --
        --- "Press the Space bar to animate" fr "Appuyez sur Espace pour animer"

    // Contents of the title
    title
        text "Tao Presentations "
        show_edition tao_edition

        // We adjust the color based on the edition of the software
        show_edition "Design Pro"       -> color "#A02"; text "Design Pro"
        show_edition "Design"           -> color "#EA1"; text "Design"
        show_edition "Player"           -> color "#063"; text "Player"
        show_edition "Player Pro"       -> color "#063"; text "Player Pro"
        show_edition Other              -> color "#036"; text Other

    // Manually create an additional text box matching the theme
    box 450, -450, 820, 90,
        theme_style theme, slide_master, "story"
        align 50%
        font_size 30
        text "© Copyright 2010-2013 Taodyne SAS"
        line_break
        font_size 20
        text "Version " & tao_version

    // Draw a small animation with a computer from which slides fly out
    draw_computer_and_pictograms


// ============================================================================
// 
//    Theme override (change the background picture)
// 
// ============================================================================

override_priority 1
theme_background "keyboard", Master:text ->
// ----------------------------------------------------------------------------
//    We override the background picture with a more neutral one
// ----------------------------------------------------------------------------
    clear_color 1, 1, 1, 1
    background -1500,
        // Slowly shifting background color
        color_hsv 6 * seconds, 0.05, 1.0
        image 0, 0, 110%, 110%, "background.png"



// ============================================================================
// 
//   Animated pictograms
// 
// ============================================================================

draw_computer_and_pictograms ->
// ----------------------------------------------------------------------------
//   Draw the MacBook and the various pictograms
// ----------------------------------------------------------------------------
    contents -1490,
        translate 200, -230, 0
        scale3 0.25
        color "white"
        image 0, 0, 100% , 100% , "MacBookPro.png"

        locally
            translate -300, 300, 200
            color "black", 0.3
            rounded_rectangle 0, 0, 600, 180, 600
            color "white"
            image 0, 0, 40% , 40%,
                if seconds mod 2 = 0 then
                    "Taodyne-Logo.png"
                else
                    "Taodyne-Logo_light.png"

        scale3 0.8
        color "white"
        rotatey 12
        rotatex 10
        locally
            if animation_duration > 0.0 then
                pictogram_time := page_time - animation_start
                if pictogram_time > animation_duration then
                    animation_duration := 0.0
            pictogram with 1..pictogram_count


// Configuration of pictograms in the directory
pictogram_count -> 28
pictogram_name N:integer -> "picto_" & text N & ".png"
pictogram_ratio N:integer -> ((N + pictogram_time)/pictogram_count mod 1)


// Configuration of the drawing of the pictograms
pictogram_time -> 0.0
pictogram_adjust S:real ->
    translate 0, 28000*S^2, 20000 * S
    scale3 2*(1-(1-S)^4)


pictogram N:integer ->
// ----------------------------------------------------------------------------
//    Draw a given pictogram
// ----------------------------------------------------------------------------
    locally
        pictogram_adjust pictogram_ratio N
        rotatez 10 * sin(191.3*N)
        image 0, 0, 1, 1, pictogram_name N



// ============================================================================
// 
//   Animation
// 
// ============================================================================

// Duration of the animation (initially 5 seconds)
animation_duration -> 20.0
animation_start    -> 0.0

// Toggle animations with space bar
key "Space" ->
    if animation_duration <= 0.0 then
        animation_duration := page_time + 30.0
        animation_start := page_time - pictogram_time
    else
        animation_duration := 0.0

